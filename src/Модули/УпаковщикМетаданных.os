#Использовать tempfiles

Перем Лог;

// Распаковать контейнер метаданных
//
// Параметры:
//   ПутьФайлаДляРаспаковки - <Строка> - путь файла для распаковки
//   КаталогРаспаковки - <Строка> - каталог для распаковки
//
Процедура РаспаковатьКонтейнерМетаданных(Знач ПутьФайлаДляРаспаковки, Знач КаталогРаспаковки) Экспорт
	ПолучитьЛог();

	ФайлДляРаспаковки = Новый файл(ПутьФайлаДляРаспаковки);
	Если КаталогРаспаковки = "" тогда
		КаталогРаспаковки = ВременныеФайлы.СоздатьКаталог();
	КонецЕсли;
	
	СтрокаЗапуска = СтрШаблон("%1 -parse ""%2"" ""%3""",
		ИмяПрограммыРаспаковки(), ФайлДляРаспаковки.ПолноеИмя, КаталогРаспаковки);
	
		ВыполнитьКоманду(СтрокаЗапуска);

	ФайлМодуля = Новый Файл(ОбъединитьПути(КаталогРаспаковки, "module"));
	Если ФайлМодуля.Существует() Тогда 
		ФайлКуда = Новый Файл(ОбъединитьПути(ФайлМодуля.Путь, "Module.bsl"));
		Лог.Отладка("Перемещаю файл <%1> в <%2>", ФайлМодуля.ПолноеИмя, ФайлКуда.ПолноеИмя);
		Если ФайлКуда.Существует() Тогда
			УдалитьФайлы(ФайлКуда.ПолноеИмя);
		КонецЕсли;
		ПереместитьФайл(ФайлМодуля.ПолноеИмя, ФайлКуда.ПолноеИмя);
	КонецЕсли; 

КонецПроцедуры

// Упаковать контейнер метаданных
//
// Параметры:
//   КаталогРаспаковки - <Строка> - каталог для распаковки
//   ПутьФайлаДляРаспаковки - <Строка> - путь файла для упаковки
//
Процедура УпаковатьКонтейнерМетаданных(Знач КаталогРаспаковки, Знач ПутьФайлаДляРаспаковки) Экспорт
	ПолучитьЛог();

	ФайлДляРаспаковки = Новый Файл(ПутьФайлаДляРаспаковки);
	Если КаталогРаспаковки = "" тогда
		КаталогРаспаковки = ВременныеФайлы.СоздатьКаталог();
	КонецЕсли;

	ФайлМодуля = Новый Файл(ОбъединитьПути(КаталогРаспаковки, "Module.bsl"));
	Если ФайлМодуля.Существует() Тогда
		КопироватьФайл(ФайлМодуля.ПолноеИмя, ОбъединитьПути(ФайлМодуля.Путь, "module") );
	КонецЕсли;
	
	СтрокаЗапуска = СтрШаблон("%1 -build -nopack ""%2"" ""%3""",
		ИмяПрограммыРаспаковки(), КаталогРаспаковки, ФайлДляРаспаковки.ПолноеИмя);

		ВыполнитьКоманду(СтрокаЗапуска);
	
КонецПроцедуры

Процедура ВыполнитьКоманду(Знач СтрокаЗапуска)
	
	Лог.Отладка(СтрокаЗапуска);

	Команда = Новый Команда;
	Команда.УстановитьПравильныйКодВозврата(0);
	Команда.УстановитьСтрокуЗапуска(СтрокаЗапуска);
	Команда.Исполнить();
КонецПроцедуры

Функция ИмяПрограммыРаспаковки()
	Рез = "v8unpack";
	Если ПараметрыСистемы.ЭтоWindows Тогда
		Рез = Рез + ".exe";
	КонецЕсли;
	Возврат Рез;
КонецФункции // ИмяПрограммыРаспаковки()

Функция ПолучитьЛог()
	Если Лог = Неопределено Тогда
		Лог = Логирование.ПолучитьЛог(ПараметрыСистемы.ИмяЛогаСистемы());
	КонецЕсли;
	Возврат Лог;	
КонецФункции
